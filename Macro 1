Option Explicit

Sub GerarDocumentoPlanilhaAtiva()
    On Error GoTo TratarErro

    Dim ws As Worksheet
    Dim WordApp As Object
    Dim WordDoc As Object
    Dim templatePath As String
    Dim nomeArquivo As String
    Dim pastaBase As String
    Dim pastaAno As String
    Dim anoEmissao As String
    Dim dataCarregamento As Date
    Dim ultimaLinha As Long, i As Long, j As Long
    Dim linhaSim As Long, linhaIDTF As Long, maisProximo As Long
    Dim idtfsEspecificos As Collection
    Dim idtfEspecificoAchado As Variant
    Dim dataEmissao, compartimentoCarga
    Dim produtoTransportado, dataDescarregamento, localEntrega
    Dim dataLimpeza, horaLimpeza, tipoLimpeza, responsavel

    ' Caminho base da pasta
    pastaBase = "C:\User\Pasta\"
    If Right(pastaBase, 1) <> "\" Then pastaBase = pastaBase & "\"

    ' Caminho do template
    templatePath = "C:\Users\User\Documents\Modelos Personalizados do Office\ModeloRelatorio.dotx"

    ' Lista de IDTFs específicos
    Set idtfsEspecificos = New Collection
    idtfsEspecificos.Add 40342 ' Fertilizante Cloreto de Potássio
    idtfsEspecificos.Add 40285 ' Fertilizantes Artificiais e corretivos minerais do solo

    ' Iniciar Word
    Set WordApp = CreateObject("Word.Application")
    WordApp.Visible = True

    ' Planilha ativa
    Set ws = ActiveSheet

    ' Extrai dados da linha marcada como "sim" para preenchimento da Tabela 1 do Word
    ultimaLinha = ws.Cells(ws.Rows.Count, 7).End(xlUp).Row ' Coluna G

    For i = 2 To ultimaLinha
        If LCase(ws.Cells(i, 7).Value) = "sim" Then
            linhaSim = i

            ' Procurar IDTF específico acima
            linhaIDTF = 0
            maisProximo = 0
            For j = linhaSim - 1 To 1 Step -1
                For Each idtfEspecificoAchado In idtfsEspecificos
                    If ws.Cells(j, 6).Value = idtfEspecificoAchado Then
                        If maisProximo = 0 Or j > maisProximo Then
                            maisProximo = j
                            linhaIDTF = j
                        End If
                    End If
                Next
            Next j

            dataEmissao = ws.Cells(linhaSim, 2).Value
            compartimentoCarga = ws.Name

            ' Definir ano e caminho da pasta por ano
            If IsDate(dataEmissao) Then
                anoEmissao = Year(dataEmissao)
            Else
                anoEmissao = Year(Date) ' Caso a célula esteja vazia ou inválida
            End If

            pastaAno = pastaBase & anoEmissao & "\"

            ' Criar pasta do ano se não existir
            If Dir(pastaAno, vbDirectory) = "" Then MkDir pastaAno

            ' Criar documento Word baseado no template
            Set WordDoc = WordApp.Documents.Add(templatePath)

            ' Preencher Tabela 1
            With WordDoc.Tables(1)
                .Cell(1, 2).Range.Text = Format(dataEmissao, "dd/mm/yyyy")
                .Cell(2, 2).Range.Text = compartimentoCarga
            End With

            'Se não encontrar nenhum produto com os IDTFs informados, imprime "Verificar o mês anterior" nos campos do Template.
            If linhaIDTF = 0 Then

                ' Preencher Tabela 2 com mensagem.
                With WordDoc.Tables(2)
                    .Cell(2, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(3, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(4, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(5, 2).Range.Text = "Verificar o mês anterior"
                End With

                ' Preencher Tabela 3 com mensagem.
                With WordDoc.Tables(3)
                    .Cell(3, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(4, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(5, 2).Range.Text = "Verificar o mês anterior"
                    .Cell(6, 2).Range.Text = "Verificar o mês anterior"
                End With

                'Salva o nome do arquivo como "Sem IDTF - dd.mm.aaaa.docx"
                nomeArquivo = "Sem IDTF - " & Format(dataEmissao, "dd.mm.yyyy")
            Else
                ' Coletar dados
                dataCarregamento = ws.Cells(linhaIDTF, 2).Value
                produtoTransportado = ws.Cells(linhaIDTF, 8).Value
                dataDescarregamento = ws.Cells(linhaIDTF, 12).Value
                localEntrega = ws.Cells(linhaIDTF, 13).Value

                dataLimpeza = ws.Cells(linhaIDTF + 1, 17).Value
                horaLimpeza = ws.Cells(linhaIDTF + 1, 18).Value
                tipoLimpeza = ws.Cells(linhaIDTF + 1, 14).Value
                responsavel = ws.Cells(linhaIDTF + 1, 19).Value

                ' Preencher Tabela 2
                With WordDoc.Tables(2)
                    .Cell(2, 2).Range.Text = Format(dataCarregamento, "dd/mm/yyyy")
                    .Cell(3, 2).Range.Text = produtoTransportado
                    .Cell(4, 2).Range.Text = Format(dataDescarregamento, "dd/mm/yyyy")
                    .Cell(5, 2).Range.Text = localEntrega
                End With

                ' Preencher Tabela 3
                With WordDoc.Tables(3)
                    .Cell(3, 2).Range.Text = dataLimpeza
                    .Cell(4, 2).Range.Text = Format(horaLimpeza, "hh:mm")
                    .Cell(5, 2).Range.Text = tipoLimpeza
                    .Cell(6, 2).Range.Text = responsavel
                End With

                'Gera nome de arquivo baseado no produto e data para facilitar organização e busca
                ' facilitando organização e busca dos relatórios. Exemplo: "Cloreto de Potássio - dd.mm.aaaa.docx"
                nomeArquivo = Replace(produtoTransportado, "/", "_")
                nomeArquivo = Replace(nomeArquivo, "\", "_")
                nomeArquivo = Replace(nomeArquivo, ":", "_")
                nomeArquivo = Replace(nomeArquivo, "*", "_")
                nomeArquivo = Replace(nomeArquivo, "?", "_")
                nomeArquivo = Replace(nomeArquivo, """", "_")
                nomeArquivo = Replace(nomeArquivo, "<", "_")
                nomeArquivo = Replace(nomeArquivo, ">", "_")
                nomeArquivo = Replace(nomeArquivo, "|", "_")
                If nomeArquivo = "" Then nomeArquivo = "Documento"

                nomeArquivo = nomeArquivo & " - " & Format(dataCarregamento, "dd.mm.yyyy")

            End If

            ' Salvar documento
            Debug.Print "Salvando: " & pastaAno & nomeArquivo & ".docx"
            WordDoc.SaveAs2 pastaAno & nomeArquivo & ".docx"
            WordDoc.Close
        End If
    Next i

    WordApp.Quit
    Set WordApp = Nothing

    MsgBox "Documento(s) gerado(s) com sucesso na pasta do ano: " & pastaAno
    Exit Sub

TratarErro:
    MsgBox "Erro: " & Err.Number & " - " & Err.Description, vbCritical, "Erro na execução"
    If Not WordApp Is Nothing Then
        WordApp.Quit
        Set WordApp = Nothing
    End If
End Sub
